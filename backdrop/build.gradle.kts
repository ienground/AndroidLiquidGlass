@file:OptIn(ExperimentalWasmDsl::class)


import org.jetbrains.kotlin.gradle.ExperimentalWasmDsl
import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    alias(libs.plugins.android.multiplatform.library)
    alias(libs.plugins.kotlin.multiplatform)
    alias(libs.plugins.kotlin.compose)
    alias(libs.plugins.jetbrains.compose)
    id("com.vanniktech.maven.publish")
}

kotlin {
    androidLibrary {
        minSdk = 21
        compileSdk = 36
        namespace = "com.kyant.backdrop"
        compilerOptions {
            jvmTarget = JvmTarget.JVM_11
        }
    }

    jvm("desktop")

    js(IR) {
        browser()
    }
    wasmJs {
        browser()
    }

    macosArm64()
    iosArm64("iosArm64")
    iosSimulatorArm64("iosSimulatorArm64")

    sourceSets {
        val commonMain by getting {
            dependencies {
                implementation(libs.compose.foundation)
                implementation(libs.compose.ui)
                implementation(libs.compose.ui.graphics)
                implementation(libs.kyant.shapes)
                implementation(libs.annotations)
            }
        }

        val skikoMain by creating {
            dependsOn(commonMain)
        }

        val desktopMain by getting {
            dependsOn(skikoMain)
        }

        val macosArm64Main by getting {
            dependsOn(skikoMain)
        }

        val iosMain by creating {
            dependsOn(skikoMain)
        }

        val iosArm64Main by getting {
            dependsOn(iosMain)
        }

        val iosSimulatorArm64Main by getting {
            dependsOn(iosMain)
        }

        val jsMain by getting {
            dependsOn(skikoMain)
        }

        val wasmJsMain by getting {
            dependsOn(skikoMain)
        }

        all {
            languageSettings.enableLanguageFeature("ContextParameters")
        }
    }
}

mavenPublishing {
    publishToMavenCentral(automaticRelease = true)
    signAllPublications()

    coordinates("zone.ien.backdrop", "backdrop", "2.0.0-alpha06")
//    coordinates("io.github.kyant0", "backdrop", "2.0.0-alpha04")

    pom {
        name.set("Backdrop")
        description.set("Compose Multiplatform Liquid Glass effects")
        inceptionYear.set("2025")
        url.set("https://github.com/ienground/AndroidLiquidGlass")
//        url.set("https://github.com/Kyant0/AndroidLiquidGlass")
        licenses {
            license {
                name.set("The Apache License, Version 2.0")
                url.set("http://www.apache.org/licenses/LICENSE-2.0.txt")
                distribution.set("repo")
            }
        }
        developers {
            developer {
                id.set("ienground")
                name.set("IENGROUND")
                url.set("https://github.com/ienground")
//                id.set("Kyant0")
//                name.set("Kyant")
//                url.set("https://github.com/Kyant0")
            }
        }
        scm {
            url.set("https://github.com/ienground/AndroidLiquidGlass")
            connection.set("scm:git:git://github.com/ienground/AndroidLiquidGlass.git")
            developerConnection.set("scm:git:ssh://git@github.com/ienground/AndroidLiquidGlass.git")
//            url.set("https://github.com/Kyant0/AndroidLiquidGlass")
//            connection.set("scm:git:git://github.com/Kyant0/AndroidLiquidGlass.git")
//            developerConnection.set("scm:git:ssh://git@github.com/Kyant0/AndroidLiquidGlass.git")
        }
    }
}
